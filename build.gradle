plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '2.0.0'
}

def gitInfo = versionDetails()
version = gitInfo.lastTag.substring(1)
def implementationVersion = version + '.' + gitInfo.gitHash[0..7]

group = 'com.github.onozaty'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile)*.options*.encoding = defaultEncoding

tasks.withType(Test) {
    systemProperty('dbhost', System.getProperty('dbhost'))
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.22.0'

    implementation 'org.mybatis:mybatis:3.5.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'

    testRuntimeOnly 'org.postgresql:postgresql:42.3.4'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Implementation-Version' : implementationVersion)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mybatis-postgresql-typehandlers'
            from components.java

            pom {
                name = 'mybatis-postgresql-typehandlers'
                description = 'MyBatis TypeHandlers for PostgreSQL.'
                url = 'https://github.com/onozaty/mybatis-postgresql-typehandlers'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/onozaty/mybatis-postgresql-typehandlers.git'
                    url = 'https://github.com/onozaty/mybatis-postgresql-typehandlers'
                }
                developers {
                    developer {
                        id = "onozaty"
                        name = "Hirokazu Onozato"
                        email = "onozaty@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
